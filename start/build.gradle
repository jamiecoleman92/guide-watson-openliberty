apply plugin: 'war'
apply plugin: 'liberty'
group = 'io.openliberty.guides'
version = '1.0-SNAPSHOT'
description = "Watson Tone Analyser demonstration with Open Liberty."

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
 options.encoding = 'UTF-8'
}

buildscript {
 repositories {
  mavenCentral()
 }
 dependencies {
  classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.6.3'
 }
}

repositories {
 mavenCentral()

 maven {
  url "https://dl.bintray.com/ibm-cloud-sdks/ibm-cloud-sdk-repo"
 }
}

dependencies {
 providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
 providedCompile group: 'javax.enterprise', name: 'cdi-api', version: '2.0'
 libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version: '[17.0.0.4,)'
}

ext {
 appName = project.name
 testServerHttpPort = 9080
 testServerHttpsPort = 9443
 warContext = appName
}

liberty {
 server {
  name = "${appName}Server"
  configFile = file("src/main/liberty/config/server.xml")
  bootstrapProperties = ['default.http.port': testServerHttpPort,
   'default.https.port': testServerHttpsPort,
   'app.context.root': warContext
  ]
  packageLiberty {
   archive = "$buildDir/${appName}.zip"
   include = "usr"
  }
 }
}

war {
 archiveName = "${baseName}.${extension}"
}

task openBrowser {
 description = 'Open browser to http://localhost:8080/${warContext}'
 doLast {
  java.awt.Desktop.desktop.browse "http://localhost:${testServerHttpPort}/${appName}".toURI()
 }
}

clean.dependsOn 'libertyStop'
libertyPackage.dependsOn 'libertyStop'